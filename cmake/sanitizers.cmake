function(__init_asan)
    add_compile_options(-fsanitize=address -fno-omit-frame-pointer -g)
    if (CMAKE_C_COMPILER_ID STREQUAL "GNU")
        add_link_options(-fsanitize=address -static-libasan)
    else ()
        add_link_options(-fsanitize=address)
    endif ()
endfunction()

function(__init_tsan)
    add_compile_options(-fsanitize=thread -g -O1)
    add_link_options(-fsanitize=thread)
endfunction()

function(init_sanitizer_flags ASAN TSAN)
    if (ASAN AND TSAN)
        message(FATAL_ERROR "The address-sanitizer and the thread-sanitizer cannot be enabled at the same time!")
        return()
    endif ()

    if (ASAN)
        message(STATUS "[address-sanitizer]: enabled")
        __init_asan()
    else ()
        message(STATUS "[address-sanitizer]: disabled")
    endif ()

    if (TSAN)
        message(STATUS "[thread-sanitizer]: enabled")
        __init_tsan()
    else ()
        message(STATUS "[thread-sanitizer]: disabled")
    endif ()
endfunction()